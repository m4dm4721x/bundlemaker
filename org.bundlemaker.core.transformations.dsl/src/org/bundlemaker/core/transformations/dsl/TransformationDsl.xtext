grammar org.bundlemaker.core.transformations.dsl.TransformationDsl with org.eclipse.xtext.common.Terminals

generate transformationDsl "http://www.bundlemaker.org/core/transformations/dsl/TransformationDsl"

TransformationModel:
	(transformations+=Transformation)*;
	
Transformation:
	RemoveFrom | EmbedInto | CreateModule | ClassifyModules
;

RemoveFrom:
 'remove-from' (resourceSet=ResourceSet) ';'
;

EmbedInto:
 'embed-into' hostModule=ModuleIdentifier 'modules'  modules+=ModuleIdentifier (',' modules+=ModuleIdentifier)* ';'
;

CreateModule:
 'create-module' module=ModuleIdentifier (layer=Layer)? (from+=From)* ';'
;

ClassifyModules:
 'classify-modules' modules=STRING ('but not ' excludedModules=STRING)? 'as' classification=STRING ';'
;

Layer:
 'layer' layer=STRING
;

From:
 'from' (resourceSet=ResourceSet)
;

ResourceSet:
 moduleIdentifier=ModuleIdentifier ('include' includeResources=ResourceList)? ('exclude' excludeResources=ResourceList)?
;

terminal BMID  		: '^'?('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;

MODULEID:
  BMID("." BMID)*
;

ModuleIdentifier:
 modulename=MODULEID version=STRING
;

ResourceList:
	resources+=STRING (',' resources+=STRING)*
;




