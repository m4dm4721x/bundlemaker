package org.bundlemaker.core.osgi.manifest.rules

import org.bundlemaker.core.modules.IResourceModule;
import com.springsource.util.osgi.manifest.BundleManifest;
import org.bundlemaker.core.resource.IReference;
import org.bundlemaker.core.osgi.internal.manifest.IdentifiableBundleManifest;
import org.bundlemaker.core.osgi.exporter.ManifestConstants;
import org.bundlemaker.core.osgi.manifest.DependencyStyle;
import com.springsource.util.osgi.manifest.ImportedPackage;
import org.bundlemaker.core.osgi.internal.manifest.ReferencesCache;
import com.springsource.util.osgi.manifest.Resolution;
import org.bundlemaker.core.modules.IModule;
import org.bundlemaker.core.modules.IModuleIdentifier;
import com.springsource.util.osgi.manifest.RequiredBundle;
import com.springsource.util.osgi.VersionRange;
import org.bundlemaker.core.osgi.manifest.IManifestPreferences;
import com.springsource.util.osgi.manifest.RequiredBundle.Visibility;

/**
 * Eingabe:
 * - IModularizedSystem
 * - IResourceModule
 * - BundleManifest
 * - IExportPackagePreferences
 */
rule "strict import packages - create imported package"
when
    IManifestPreferences( dependencyStyle == DependencyStyle.STRICT_IMPORT_PACKAGE ) 
    $resourceModule : IResourceModule()
    $bundleManifest : IdentifiableBundleManifest( role == IdentifiableBundleManifest.BUNDLE_MANIFEST )
    $referencedPackageName : String() from $resourceModule.getReferencedPackageNames(true, true, true)
then
   // create a new importedPackage
   ImportedPackage importedPackage = $bundleManifest.getImportPackage().addImportedPackage($referencedPackageName);
   insert( importedPackage );
end

rule "set imported package optional if not exporter exists"
when
    $importedPackage : ImportedPackage()
    $referencesCache : ReferencesCache()
    eval( $referencesCache.hasExportingModules($importedPackage.getPackageName()) )
then
   $importedPackage.setResolution(Resolution.OPTIONAL);
end

rule "RequiredBundle: strict require bundle - "
when
    IManifestPreferences( dependencyStyle == DependencyStyle.STRICT_REQUIRE_BUNDLE ) 
    $bundleManifest : IdentifiableBundleManifest( role == IdentifiableBundleManifest.BUNDLE_MANIFEST )
    $resourceModule : IResourceModule()
    $referencesCache : ReferencesCache()
    $modularizedSystem : IModularizedSystem()
    $referencedPackageName : String() from $resourceModule.getReferencedPackageNames(true, true, true)
    $exportingModule : IModule() from $referencesCache.getExportingModules($referencedPackageName)
    $exportingHostModule : IModule() from ManifestUtils.getFragmentHost($exportingModule)
    $symbolicName : String() from $exportingHostModule.getModuleIdentifier().getName()
    not ( RequiredBundle(bundleSymbolicName == $symbolicName) )
    not ( eval ( ManifestUtils.isFragment($resourceModule) && ManifestUtils.getFragmentHost($resourceModule).equals($exportingHostModule)))
then
   // create a required bundle
   RequiredBundle requiredBundle = $bundleManifest.getRequireBundle().addRequiredBundle($symbolicName);
   // set a version range if multiple versions exist
   if ($modularizedSystem.getModules($symbolicName).size() > 1) {
     requiredBundle.setBundleVersion(new VersionRange("[" + $exportingHostModule.getModuleIdentifier().getVersion() + "," + $exportingHostModule.getModuleIdentifier().getVersion() + "]"));
   }
   // set the visibility to reexport (TODO!!)
   requiredBundle.setVisibility(Visibility.REEXPORT);
   insert( requiredBundle );
end

rule "RequiredBundle - Template"
salience 50
when
    $bundleManifest : IdentifiableBundleManifest( role == IdentifiableBundleManifest.BUNDLE_MANIFEST )
    $bundleManifestTemplate : IdentifiableBundleManifest( role == IdentifiableBundleManifest.MANIFEST_TEMPLATE )
    $requiredBundle :  RequiredBundle() from $bundleManifestTemplate.getRequireBundle().getRequiredBundles();
    not ( RequiredBundle(bundleSymbolicName == $requiredBundle.bundleSymbolicName) )
then
    $bundleManifest.getRequireBundle().getRequiredBundles().add($requiredBundle);
    insert( $requiredBundle );
end

rule "RequiredBundle: replace require bundle 'system.bundle' "
salience -50
when
   $modularizedSystem : IModularizedSystem()
   $bundleManifest : IdentifiableBundleManifest( role == IdentifiableBundleManifest.BUNDLE_MANIFEST )
   $symbolicName : String() from $modularizedSystem.getExecutionEnvironment().getModuleIdentifier().getName()
   $requiredBundle : RequiredBundle(bundleSymbolicName == $symbolicName)
then
   // create a required bundle
   $bundleManifest.getRequireBundle().getRequiredBundles().remove($requiredBundle);
   RequiredBundle systemBundle = $bundleManifest.getRequireBundle().addRequiredBundle("system.bundle");
   systemBundle.setVisibility(Visibility.REEXPORT);
end
